#Android将Library上传到jcenter超简单完整步骤以及遇到的各种坑

>在[【玩转SQLite系列】(七)打造轻量级ORM工具类SQLiteDbUtil操作数据库](http://blog.csdn.net/linglongxin24/article/details/53385868)
中我们可以看到这个工具类的超简洁用法：只需要在gradle中引入：

```gradle
compile 'cn.bluemobi.dylan:sqlitelibrary:0.1'
```
>即可使用，那么这到底是怎么回事？这个是Android项目所在jcenter的一个远程仓库。我们只需要将你的libray上传到jcenter即可，那么如和将一个项目上传到jcenter呢？

#1.去jcenter官网注册个账号：https://bintray.com

![注册界面](https://github.com/linglongxin24/SQLite/blob/master/screenshorts/jcenter_sign_up.png?raw=true)

#2.在项目根目录的build.gradle(Project)添加配置

```gradle
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.2'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.5'
```

![在项目根目录的配置](https://github.com/linglongxin24/SQLite/blob/master/screenshorts/jcenter_project_build.gradle.png?raw=true)

#3.在你要上传的module的build.gradle中添加如下配置


![在library根目录的配置](https://github.com/linglongxin24/SQLite/blob/master/screenshorts/jcenter_config_modle_build_gradle.png?raw=true)


```gradle

/** 以下开始是将Android Library上传到jcenter的相关配置**/

apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

//项目主页
def siteUrl = 'https://github.com/linglongxin24/SQLite'    // project homepage
//项目的版本控制地址
def gitUrl = 'https://github.com/linglongxin24/SQLite.git' // project git

//发布到组织名称名字，必须填写
group = "cn.bluemobi.dylan"
//发布到JCenter上的项目名字，必须填写
def libName = "AndroidFastDevFrame"
// 版本号，下次更新是只需要更改版本号即可
version = "1.0"
/**  上面配置后上传至jcenter后的编译路径是这样的： compile 'cn.bluemobi.dylan:sqlitelibrary:1.0'  **/

//生成源文件
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
//生成文档
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    options.encoding "UTF-8"
    options.charSet 'UTF-8'
    options.author true
    options.version true
    options.links "https://github.com/linglongxin24/FastDev/tree/master/mylibrary/docs/javadoc"
    failOnError false
}

//文档打包成jar
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
//拷贝javadoc文件
task copyDoc(type: Copy) {
    from "${buildDir}/docs/"
    into "docs"
}

//上传到jcenter所需要的源码文件
artifacts {
    archives javadocJar
    archives sourcesJar
}

// 配置maven库，生成POM.xml文件
install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                name 'This is a lightweight SQLite database framework'
                url siteUrl
                licenses {
                    license {
                        name 'This is a lightweight SQLite database framework'
                        url 'https://github.com/linglongxin24/SQLite'
                    }
                }
                developers {
                    developer {
                        id 'linglongxin24'
                        name 'dylan'
                        email 'linglongxin24@163.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

//上传到jcenter
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintray.user")    //读取 local.properties 文件里面的 bintray.user
    key = properties.getProperty("bintray.apikey")   //读取 local.properties 文件里面的 bintray.apikey
    configurations = ['archives']
    pkg {
        repo = "maven"
        name = libName    //发布到JCenter上的项目名字，必须填写
        desc = 'This is a lightweight SQLite database framework'    //项目描述
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }

```

#4.在你项目根目录的local.properties中添加用户名和key
 * ![edit](https://github.com/linglongxin24/SQLite/blob/master/screenshorts/jcenter_edit_profile.png?raw=true)
 * ![key](https://github.com/linglongxin24/SQLite/blob/master/screenshorts/jcenter_get_api_key.png?raw=true)

 ```gradle
 bintray.user=linglongxin24
 bintray.apikey=xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
 ```

#5.执行命令

```
graedlew install
graedlew bintrayUpload
```



